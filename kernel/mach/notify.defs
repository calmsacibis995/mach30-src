/* 
 * Mach Operating System
 * Copyright (c) 1991,1990,1989 Carnegie Mellon University
 * All Rights Reserved.
 * 
 * Permission to use, copy, modify and distribute this software and its
 * documentation is hereby granted, provided that both the copyright
 * notice and this permission notice appear in all copies of the
 * software, derivative works or modified versions, and any portions
 * thereof, and that both notices appear in supporting documentation.
 * 
 * CARNEGIE MELLON ALLOWS FREE USE OF THIS SOFTWARE IN ITS "AS IS"
 * CONDITION.  CARNEGIE MELLON DISCLAIMS ANY LIABILITY OF ANY KIND FOR
 * ANY DAMAGES WHATSOEVER RESULTING FROM THE USE OF THIS SOFTWARE.
 * 
 * Carnegie Mellon requests users of this software to return to
 * 
 *  Software Distribution Coordinator  or  Software.Distribution@CS.CMU.EDU
 *  School of Computer Science
 *  Carnegie Mellon University
 *  Pittsburgh PA 15213-3890
 * 
 * any improvements or extensions that they make and grant Carnegie Mellon
 * the rights to redistribute these changes.
 */
/*
 * HISTORY
 * $Log:	notify.defs,v $
 * Revision 2.6  91/08/28  11:15:33  jsb
 * 	Added conditionalized sequence number support.
 * 	[91/08/13            rpd]
 * 
 * Revision 2.5  91/05/14  16:58:11  mrt
 * 	Correcting copyright
 * 
 * Revision 2.4  91/02/05  17:35:14  mrt
 * 	Changed to new Mach copyright
 * 	[91/02/01  17:19:54  mrt]
 * 
 * Revision 2.3  90/08/27  22:04:34  dbg
 * 	Fixed the notification ports, to make them send-once rights.
 * 	[90/08/13            rpd]
 * 
 * Revision 2.2  90/06/02  14:59:28  rpd
 * 	Created for new IPC.
 * 	[90/03/26  23:45:48  rpd]
 * 
 */

subsystem notify 64;

#include <mach/std_types.defs>

#if	SEQNOS
serverprefix do_seqnos_;
serverdemux seqnos_notify_server;
#else	SEQNOS
serverprefix do_;
serverdemux notify_server;
#endif	SEQNOS

type notify_port_t = MACH_MSG_TYPE_MOVE_SEND_ONCE
	ctype: mach_port_t;

/* MACH_NOTIFY_FIRST: 0100 */
skip;

/* MACH_NOTIFY_PORT_DELETED: 0101 */
simpleroutine mach_notify_port_deleted(
		notify	: notify_port_t;
#if	SEQNOS
	msgseqno seqno	: mach_port_seqno_t;
#endif	SEQNOS
		name	: mach_port_name_t);

/* MACH_NOTIFY_MSG_ACCEPTED: 0102 */
simpleroutine mach_notify_msg_accepted(
		notify	: notify_port_t;
#if	SEQNOS
	msgseqno seqno	: mach_port_seqno_t;
#endif	SEQNOS
		name	: mach_port_name_t);

skip;	/* was NOTIFY_OWNERSHIP_RIGHTS: 0103 */

skip;	/* was NOTIFY_RECEIVE_RIGHTS: 0104 */

/* MACH_NOTIFY_PORT_DESTROYED: 0105 */
simpleroutine mach_notify_port_destroyed(
		notify	: notify_port_t;
#if	SEQNOS
	msgseqno seqno	: mach_port_seqno_t;
#endif	SEQNOS
		rights	: mach_port_receive_t);

/* MACH_NOTIFY_NO_SENDERS: 0106 */
simpleroutine mach_notify_no_senders(
		notify	: notify_port_t;
#if	SEQNOS
	msgseqno seqno	: mach_port_seqno_t;
#endif	SEQNOS
		mscount	: mach_port_mscount_t);

/* MACH_NOTIFY_SEND_ONCE: 0107 */
simpleroutine mach_notify_send_once(
		notify	: notify_port_t
#if	SEQNOS
;	msgseqno seqno	: mach_port_seqno_t
#endif	SEQNOS
		);

/* MACH_NOTIFY_DEAD_NAME: 0110 */
simpleroutine mach_notify_dead_name(
		notify	: notify_port_t;
#if	SEQNOS
	msgseqno seqno	: mach_port_seqno_t;
#endif	SEQNOS
		name	: mach_port_name_t);
